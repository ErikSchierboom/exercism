needs string/repinsert

: chars \ s -- a
  "" s:/
;

: letters \ a
  "abcdefghijklmnopqrstuvwxyz" "" s:/
;

: letter> \ s -- n
  letters swap ' s:= a:indexof nip
;

: >letter \ n -- s
  letters swap a:@ nip
;

: enc-letter \ n n s -- s
  letter> rot n:* n:+ 26 n:mod >letter
;

: enc \ n n s s -- n n s
  dup /[[:digit:]]/ r:match if drop s:+ ;then drop
  dup /[[:alpha:]]/ r:match if drop 2over rot enc-letter s:+ ;then drop
  drop
;

: coprime? \ n - T
  26 n:gcd 1 n:=
;

: chunk \ s -- s
  5 " " s:repinsert
;

: >code \ s n n -- s
  over coprime? !if 3drop null ;then
  rot s:lc chars ' enc "" a:reduce chunk 2nip
;

: dec-letter \ n n s -- s
  letter> swap n:- swap 26 n:invmod n:* 26 n:mod >letter
;

: dec \ n n s s -- n n s
  dup /[[:digit:]]/ r:match if drop s:+ ;then drop
  dup /[[:alpha:]]/ r:match if drop 2over rot dec-letter s:+ ;then drop
  drop
;

: code> \ s n n -- s
  over coprime? !if 3drop null ;then
  rot chars ' dec "" a:reduce
;
