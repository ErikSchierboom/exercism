#
# Test atbash_cipher with some examples
#
# a0 - input string, for callee
# a1 - pointer to output string, for callee
# s0 - num of tests left to run
# s1 - address of input string
# s2 - address of expected output string
# s3 - char byte of input
# s4 - char byte of output
# s5 - copy of output location

.data

# number of test cases
n: .word 5
# input values and expected output values (all null terminated)
ins:  .asciiz "yes", "no", "OMG", "mindblowingly",   "The quick brown fox jumps over the lazy dog."
outs: .asciiz "bvh", "ml", "lnt", "nrmwy oldrm tob", "gsvjf rxpyi ldmul cqfnk hlevi gsvoz abwlt"

failmsg: .asciiz "failed for test input: "
expectedmsg: .asciiz ". expected "
tobemsg: .asciiz " to be "
okmsg: .asciiz "all tests passed"


.text

runner:
        lw      $s0, n
        la      $s1, ins
        la      $s2, outs

        li      $v0, 9                  # code for allocating heap memory
        li      $a0, 44                 # specify 44 bytes - length of longest expected output
        syscall
        move    $s5, $v0                # location of allocated memory is where callee writes result

run_test:
        jal     clear_output            # zero out output location
        move    $a0, $s1                # load input value into a0
        move    $a1, $s5                # load destination address into a1
        jal     atbash_cipher           # call subroutine under test
        move    $a1, $s5
        move    $s6, $s5                # take copy of output value
        move    $s7, $s2

scan:
        lb      $s3, 0($s2)             # load one byte of the expectation
        lb      $s4, 0($a1)             # load one byte of the actual
        bne     $s3, $s4, exit_fail     # if the two differ, the test has failed
        addi    $s2, $s2, 1             # point to next expectation byte
        addi    $a1, $a1, 1             # point to next actual byte
        bne     $s3, $zero, scan        # if one char (and therefore the other) was not null, loop

input_scan:
        addi    $s1, $s1, 1
        lb      $s3, 0($s1)
        bne     $s3, $zero, input_scan

done_scan:
        addi    $s1, $s1, 1             # point to next input word
        sub     $s0, $s0, 1             # decrement num of tests left to run
        bgt     $s0, $zero, run_test    # if more than zero tests to run, jump to run_test

exit_ok:
        la      $a0, okmsg              # put address of okmsg into a0
        li      $v0, 4                  # 4 is print string
        syscall

        li      $v0, 10                 # 10 is exit with zero status (clean exit)
        syscall

exit_fail:
        la      $a0, failmsg            # put address of failmsg into a0
        li      $v0, 4                  # 4 is print string
        syscall

        move    $a0, $s1                # print input that failed on
        li      $v0, 4
        syscall

        la      $a0, expectedmsg
        li      $v0, 4
        syscall

        move    $a0, $s6                # print actual that failed on
        li      $v0, 4
        syscall

        la      $a0, tobemsg
        li      $v0, 4
        syscall

        move    $a0, $s7                # print expected value that failed on
        li      $v0, 4
        syscall

        li      $a0, 1                  # set error code to 1
        li      $v0, 17                 # 17 is exit with error
        syscall

clear_output:
        sw      $zero, 0($s5)           # zero out output by storing 4 words (16 bytes) of zeros
        sw      $zero, 4($s5)
        sw      $zero, 8($s5)
        sw      $zero, 12($s5)
        sw      $zero, 16($s5)
        sw      $zero, 20($s5)
        sw      $zero, 24($s5)
        sw      $zero, 28($s5)
        sw      $zero, 32($s5)
        sw      $zero, 36($s5)
        sw      $zero, 40($s5)
        jr      $ra

# # Include your implementation here if you wish to run this from the MARS GUI.
# .include "impl.mips"
