.globl valid

valid:
        move $t0, $a0
        li   $t9, 0
__count_loop:
        lb   $t1, ($t0)
        addi $t0, $t0, 1
        beqz $t1, __validated
        beq  $t1, ' ', __count_loop
        blt  $t1, '0', __invalid
        bgt  $t1, '9', __invalid
        addi $t9, $t9, 1
        j    __count_loop
__validated:
        li   $t1, 1
        ble  $t9, $t1, __invalid
        sub  $t0, $t0, 1
        li   $t8, 0
        li   $t7, 0
__sum_loop:
        sub  $t0, $t0, 1
        blt  $t0, $a0, __check_sum        
        lb   $t1, ($t0)
        beq  $t1, ' ', __sum_loop
        sub  $t1, $t1, '0'
        beqz $t8, __single
        li   $t2, 2
        mul  $t1, $t1, $t2
        li   $t2, 9
        ble  $t1, $t2, __single
        sub  $t1, $t1, 9
__single:
        add  $t7, $t7, $t1
        nor  $t8, $t8, $zero
        j    __sum_loop
__check_sum:
        li   $t0, 10
        rem  $t0, $t7, $t0
        bnez $t0, __invalid
        li   $v0, 1  
        j  __return
__invalid:
        li $v0, 0
__return:
        jr $ra
